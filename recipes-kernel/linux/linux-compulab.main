SUMMARY = "CompuLab Linux Kernel for iMX9 family"

inherit kernel-yocto kernel fsl-kernel-localversion fsl-vivante-kernel-driver-handler

LICENSE = "GPL-2.0-only"
LIC_FILES_CHKSUM = "file://COPYING;md5=6bc538ed5bd9a7fc9398086aedcd7e46"

S = "${WORKDIR}/git"

# Tell to kernel class that we would like to use our defconfig to configure the kernel.
# Otherwise, the --allnoconfig would be used per default which leads to mis-configured
# kernel.
#
# This behavior happens when a defconfig is provided, the kernel-yocto configuration
# uses the filename as a trigger to use a 'allnoconfig' baseline before merging
# the defconfig into the build.
#
# If the defconfig file was created with make_savedefconfig, not all options are
# specified, and should be restored with their defaults, not set to 'n'.
# To properly expand a defconfig like this, we need to specify: KCONFIG_MODE="--alldefconfig"
# in the kernel recipe include.
KCONFIG_MODE="--alldefconfig"

# We need to pass it as param since kernel might support more then one
# machine, with different entry points
KERNEL_EXTRA_ARGS += "LOADADDR=${UBOOT_ENTRYPOINT}"

DEPENDS += "lzop-native bc-native"

require linux-compulab_${PV}.inc

DEFAULT_PREFERENCE = "1"

KERNEL_VERSION_SANITY_SKIP="1"

# Merged from the bbappend
FILESEXTRAPATHS:prepend := "${THISDIR}/compulab/${PV}/imx93:"

require compulab/${PV}/imx93.inc

KERNEL_CONFIG = "${MACHINE}_defconfig compulab.config"
EXTRA_OEMAKE += "DTC_FLAGS=-@ "

do_configure:prepend() {
    # Configuration fragments add support
    for cfg_frag in $(ls ${WORKDIR}/*.cfg 2>/dev/null);do
        cat ${cfg_frag} >> ${S}/arch/arm64/configs/compulab.config
    done
}

do_configure:append() {
    if [ -z ${SKIP_MACHINE_DEFCONFIG} ];then
        if [ -z ${KERNEL_CONFIG} ];then
            oe_runmake ${MACHINE}_defconfig
        else
            oe_runmake ${KERNEL_CONFIG}
        fi
    fi
}

do_compile:prepend() {
    export SOURCE_DATE_EPOCH=$(date +%s)
}

do_install:append() {
    cd ${D}/${KERNEL_IMAGEDEST}
    ln -s Image-${KERNEL_VERSION} kernel-${KERNEL_VERSION}
    cd -
    #install -m 0644 ${KERNEL_OUTPUT_DIR}/Image ${D}/${KERNEL_IMAGEDEST}/kernel-${KERNEL_VERSION}
    oe_runmake headers_install INSTALL_HDR_PATH=${D}${exec_prefix}/src/linux-${KERNEL_VERSION} ARCH=$ARCH
}

FILES:${KERNEL_PACKAGE_NAME}-image:append = " /boot/kernel-${KERNEL_VERSION} "

do_kernel_localversion:prepend() {
    touch ${WORKDIR}/defconfig
}

PACKAGES =+ "linux-compulab-headers"
FILES:linux-compulab-headers = "${exec_prefix}/src/linux*"

PACKAGESPLITFUNCS:remove = "split_kernel_module_packages"

FILES:${KERNEL_PACKAGE_NAME}-modules = "/lib/modules/ /etc/"

COMPATIBLE_MACHINE = "ucm-imx93"
